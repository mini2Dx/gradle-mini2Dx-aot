import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.9.0'
	}
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://oss.sonatype.org/service/local/repositories/releases/content/" }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

if(project.hasProperty('ossrhUser') && project.hasProperty("release")) {
	apply plugin: "io.codearte.nexus-staging"

	nexusStaging {
		username = ossrhUser
		password = ossrhPassword
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.mini2Dx'
version = '1.0.0-SNAPSHOT'
description = 'mini2Dx gradle plugin for generating mini2Dx AOT metadata'

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile 'org.reflections:reflections:0.9.12'
	implementation 'org.mini2Dx:mini2dx-core:2.0.0-alpha.43'

	testCompile 'junit:junit:4.11'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

// Maven Central publication
if(project.hasProperty('ossrhUser')) {
	signing {
		sign configurations.archives
	}
	
	uploadArchives {
		repositories {
			mavenDeployer {
				// POM signature
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				// Target repository
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUser, password: ossrhPassword)
				}
				pom.project {
					name project.name
					description project.description
					packaging 'jar'
					url 'https://github.com/mini2Dx/gradle-mini2Dx-aot'

					scm {
						connection 'scm:git:https://github.com/mini2Dx/gradle-mini2Dx-aot.git'
						developerConnection 'scm:git:git@github.com:mini2Dx/gradle-mini2Dx-aot.git'
						url 'https://github.com/mini2Dx/gradle-mini2Dx-aot.git'
					}

					licenses {
						license {
							name 'Apache License 2.0'
							url 'https://opensource.org/licenses/Apache-2.0'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id = 'tomcashman'
							name = 'Thomas Cashman'
							email = 'thomascashman404@gmail.com'
						}
					}
				}
			}
		}
	}
}
